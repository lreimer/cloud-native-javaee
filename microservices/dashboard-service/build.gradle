plugins {
    id 'java'
    id 'war'
}

repositories {
    jcenter()
}

configurations {
    activemq {
        description = "Active MQ dependencies"
        transitive = true
    }
    opentracing {
        description = "Opentracing dependencies"
        transitive = true
    }

    providedCompile.extendsFrom opentracing
}

dependencies {
    providedCompile 'javax:javaee-api:8.0'

    providedCompile 'org.eclipse.microprofile.health:microprofile-health-api:1.0'
    providedCompile 'org.eclipse.microprofile.metrics:microprofile-metrics-api:1.0'
    providedCompile 'org.eclipse.microprofile.fault-tolerance:microprofile-fault-tolerance-api:1.0'

    runtime 'de.qaware.oss.logging:logging-jsr346:1.0'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    activemq 'org.apache.activemq:activemq-rar:5.15.3@rar'

    runtime 'de.qaware.oss.tracing:tracing-jsr346:1.0'
    compile 'io.opentracing.contrib:opentracing-cdi:0.1.0'
    compile 'io.opentracing.contrib:opentracing-jaxrs2:0.1.5'
    compile 'io.opentracing.contrib:opentracing-jms-2:0.0.7'

    opentracing 'io.opentracing:opentracing-api:0.31.0'
    opentracing 'io.jaegertracing:jaeger-core:0.28.0'
    opentracing 'io.jaegertracing:jaeger-tracerresolver:0.28.0'
}

task copyActiveMqLibs(type: Copy) {
    from configurations.activemq
    into "$buildDir/activemq"
}

task copyOpenTracingLibs(type: Copy) {
    from configurations.opentracing
    into "$buildDir/opentracing"
}

build.dependsOn copyActiveMqLibs, copyOpenTracingLibs
