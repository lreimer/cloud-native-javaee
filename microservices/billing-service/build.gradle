plugins {
    id 'java'
    id 'war'
}

repositories {
    jcenter()
}

configurations {
    activemq {
        description = "Active MQ dependencies"
        transitive = true
    }
    postgresql {
        description = "PostgreSQL dependencies"
        transitive = true
    }
    hazelcast {
        description = "Hazelcast dependencies"
        transitive = true
    }
    libext {
        description = "lib/ext dependencies"
        transitive = true
    }
    opentracing {
        description = "Opentracing dependencies"
        transitive = true
    }

    providedCompile.extendsFrom postgresql, opentracing
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'

    providedCompile 'org.eclipse.microprofile.health:microprofile-health-api:1.0'
    providedCompile 'org.eclipse.microprofile.metrics:microprofile-metrics-api:1.0'

    providedCompile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.2'

    providedRuntime 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
    providedRuntime 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.5'

    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.apache.deltaspike.core:deltaspike-core-api:1.8.0'
    compile 'org.apache.deltaspike.core:deltaspike-core-impl:1.8.0'

    runtime 'de.qaware.oss.logging:logging-jsr346:1.0'
    runtime 'org.slf4j:slf4j-api:1.7.24'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    activemq 'org.apache.activemq:activemq-rar:5.15.3@rar'
    postgresql 'org.postgresql:postgresql:42.2.2'

    // hazelcast 'com.hazelcast:hazelcast-kubernetes:1.1.0'
    hazelcast 'org.slf4j:slf4j-jdk14:1.7.25'
    hazelcast(group: 'com.hazelcast', name: 'hazelcast-kubernetes', version: '1.1.0') {
        exclude module: 'hazelcast'
        exclude module: 'jackson-annotations'
        exclude module: 'jackson-core'
        exclude module: 'jackson-databind'
        exclude module: 'jackson-module-jaxb-annotations'
        exclude module: 'validation-api'
        exclude module: 'jul-to-slf4j'
    }

    runtime 'de.qaware.oss.tracing:tracing-jsr346:1.0'
    compile 'io.opentracing.contrib:opentracing-cdi:0.1.0'
    compile 'io.opentracing.contrib:opentracing-jaxrs2:0.1.5'
    compile 'io.opentracing.contrib:opentracing-jms-2:0.0.7'

    opentracing 'io.opentracing:opentracing-api:0.31.0'
    opentracing 'io.jaegertracing:jaeger-core:0.28.0'
    opentracing 'io.jaegertracing:jaeger-tracerresolver:0.28.0'
}

task copyActiveMqLibs(type: Copy) {
    from configurations.activemq
    into "$buildDir/activemq"
}

task copyPostgresqlLibs(type: Copy) {
    from configurations.postgresql
    into "$buildDir/postgresql"
}

task copyHazelcastLibs(type: Copy) {
    from configurations.hazelcast
    into "$buildDir/hazelcast"
}

task copyOpenTracingLibs(type: Copy) {
    from configurations.opentracing
    into "$buildDir/opentracing"
}

build.dependsOn copyActiveMqLibs, copyPostgresqlLibs, copyHazelcastLibs, copyOpenTracingLibs
