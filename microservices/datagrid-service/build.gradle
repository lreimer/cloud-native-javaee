plugins {
    id 'java'
    id 'war'
}

repositories {
    jcenter()
}

configurations {
    hazelcast {
        description = "Hazelcast dependencies"
        transitive = true
    }
    opentracing {
        description = "Opentracing dependencies"
        transitive = true
    }

    providedCompile.extendsFrom opentracing
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
    providedCompile 'javax.cache:cache-api:1.0.0'

    // hazelcast 'com.hazelcast:hazelcast-kubernetes:1.1.0'
    hazelcast 'org.slf4j:slf4j-jdk14:1.7.24'
    hazelcast(group: 'com.hazelcast', name: 'hazelcast-kubernetes', version: '1.1.0') {
        exclude module: 'hazelcast'
        exclude module: 'jackson-annotations'
        exclude module: 'jackson-core'
        exclude module: 'jackson-databind'
        exclude module: 'jackson-module-jaxb-annotations'
        exclude module: 'validation-api'
        exclude module: 'jul-to-slf4j'
    }

    runtime 'de.qaware.oss.tracing:tracing-jsr346:1.0'
    compile 'io.opentracing.contrib:opentracing-cdi:0.1.0'
    compile 'io.opentracing.contrib:opentracing-jaxrs2:0.1.5'
    compile 'io.opentracing.contrib:opentracing-jms-2:0.0.7'

    opentracing 'io.opentracing:opentracing-api:0.31.0'
    opentracing 'io.jaegertracing:jaeger-core:0.28.0'
    opentracing 'io.jaegertracing:jaeger-tracerresolver:0.28.0'
}

task copyHazelcastLibs(type: Copy) {
    from configurations.hazelcast
    into "$buildDir/hazelcast"
}

task copyOpenTracingLibs(type: Copy) {
    from configurations.opentracing
    into "$buildDir/opentracing"
}

build.dependsOn copyHazelcastLibs, copyOpenTracingLibs
